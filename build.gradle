buildscript {
    ext.kotlin_version = '1.6.21'
    ext.KOTLIN_BOM = "org.jetbrains.kotlin:kotlin-bom:${kotlin_version}"
    ext.SPACESHIP_BOM = 'it.scoppelletti.spaceship:spaceship-bom:1.0.0'

    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath platform(KOTLIN_BOM)
        classpath platform(SPACESHIP_BOM)
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.google.devtools.ksp' version '1.6.21-1.0.6'
    id 'org.jetbrains.dokka' version '1.6.21'
}

group = 'it.scoppelletti.spaceship'
version = '1.0.0'
description 'Spaceship Gradle Plugins'
archivesBaseName = 'spaceship-gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Simulate Spaceship extension
def developerName = 'Dario Scoppelletti'
def developerEmail = 'dario@scoppelletti.it'
def developerUrl = 'http://www.scoppelletti.it'
def copyrightYear = '2019-2022'
def licenseName = 'The Apache License, Version 2.0'
def licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

gradlePlugin {
    plugins {
        spaceshipJavaLibrary {
            id = 'it.scoppelletti.spaceship.java-library'
            implementationClass = 'it.scoppelletti.spaceship.gradle.java.LibraryPlugin'
        }
    }
}

kotlin {
    explicitApi = 'strict'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

repositories {
    mavenCentral()

    if (project.hasProperty('it.scoppelletti.spaceship.repository.url')) {
        maven {
            url project.property('it.scoppelletti.spaceship.repository.url')
        }
    }
}

configurations {
    bom
    api.extendsFrom(bom)
    ksp.extendsFrom(bom)
    implementation.extendsFrom(bom)
    testImplementation.extendsFrom(bom)
}

dependencies {
    bom platform(KOTLIN_BOM)
    bom platform(SPACESHIP_BOM)
    api gradleApi()
    api 'org.freemarker:freemarker'
    api 'org.jetbrains.dokka:dokka-gradle-plugin'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.slf4j:slf4j-api'
    implementation 'com.squareup.moshi:moshi'
    ksp 'com.squareup.moshi:moshi-kotlin-codegen'
    testImplementation gradleTestKit()
    testImplementation 'io.kotest:kotest-assertions-core'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

dokkaHtml {
    configure {
        outputDirectory.set(project.file('build/dokka'))
        moduleName.set(project.name)
        moduleVersion.set(project.version)
        suppressObviousFunctions.set(false)
        suppressInheritedMembers.set(true)
        offlineMode.set(true)
        dokkaSourceSets {
            named('main') {
                documentedVisibilities.set([
                        DokkaConfiguration.Visibility.PUBLIC,
                        DokkaConfiguration.Visibility.PROTECTED])
                skipDeprecated.set(false)
                reportUndocumented.set(false)
                skipEmptyPackages.set(true)
                platform.set(Platform.jvm)
                noJdkLink.set(true)
                noAndroidSdkLink.set(true)
                suppressGeneratedFiles.set(true)
                includes.from('README.md')
            }
            pluginsMapConfiguration.set([
                    "org.jetbrains.dokka.base.DokkaBase": """{
"footerMessage" : "Copyright(C) ${copyrightYear} <a href='${developerUrl}'>${developerName}</a>",
"customStyleSheets" : [ "${project.file('etc/logo-styles.css')}" ]
}"""
                    ])
        }
    }
}

tasks.register('copyLicense', Copy) {
    description = 'Copies the license file'
    group = BasePlugin.BUILD_GROUP
    from project.rootDir
    into project.file('build/metainf')
    include 'LICENSE'
    rename('LICENSE', 'LICENSE.txt')
}

tasks.register('generateNotice') {
    dependsOn('copyLicense')
    description = 'Creates the notice file'
    group = BasePlugin.BUILD_GROUP
    doLast {
        def file = project.file('build/metainf/NOTICE.txt')
        file.write "${project.description}\n"
        file.append "Copyright(C) ${copyrightYear} ${developerName}, ${developerUrl}\n"
    }
}

jar {
    dependsOn('generateNotice')
    metaInf { spec -> spec.from(project.file('build/metainf')) }
}

tasks.register('generateSourcesPackage', Jar) {
    dependsOn('generateNotice')
    description = 'Generates the sources package'
    group = PublishingPlugin.PUBLISH_TASK_GROUP
    from(project.file('src/main'))
    metaInf { spec -> spec.from(project.file('build/metainf')) }
    destinationDirectory.set(project.file('build/libs'))
    archiveBaseName.set('src')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('sources')
    archiveExtension.set('jar')
}

tasks.register("generateJavadocPackage", Jar) {
    dependsOn('dokkaHtml', 'generateNotice')
    description = 'Generates the Javadoc package'
    group = PublishingPlugin.PUBLISH_TASK_GROUP
    from(project.file('build/dokka'))
    metaInf { spec -> spec.from(project.file('build/metainf')) }
    destinationDirectory.set(project.file('build/libs'))
    archiveBaseName.set('javadoc')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('javadoc')
    archiveExtension.set('jar')
}

publishing {
    publications {
        // publishLibraryPublicationToDevRepository
        library(MavenPublication) {
            from components.java
            artifact generateSourcesPackage
            artifact generateJavadocPackage

            artifactId = archivesBaseName

            pom {
                name = rootProject.name
                description = project.description
                url = 'http://github.com/dscoppelletti/spaceship-gradle'
                inceptionYear = copyrightYear
                developers {
                    developer {
                        name = developerName
                        email = developerEmail
                        url = developerUrl
                    }
                }
                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dscoppelletti/spaceship-gradle.git'
                    developerConnection = 'scm:git:git@github.com:dscoppelletti/spaceship-gradle.git'
                    url = 'git@github.com:dscoppelletti/spaceship-gradle.git'
                }
            }
        }
    }

    if (project.hasProperty('it.scoppelletti.spaceship.repository.url')) {
        repositories {
            maven {
                name = 'dev'
                url = project.property('it.scoppelletti.spaceship.repository.url')
            }
        }
    }
}
